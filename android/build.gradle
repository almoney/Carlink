buildscript {
    // Apply simplified project configuration inside buildscript
    apply from: '../project_config.gradle'
    ext.kotlin_version = myKotlinVersion
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:${myGradleVersion}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.23.7"
    id "org.jlleitschuh.gradle.ktlint" version "12.1.1"
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

// Apply simplified project configuration at project level
apply from: '../project_config.gradle'

group myLibraryGroup
version myLibraryVersion

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

// Configure detekt for static analysis
detekt {
    buildUponDefaultConfig = true
    allRules = false
    config.setFrom(files("${rootProject.projectDir}/../detekt.yml"))
    baseline = file("${rootProject.projectDir}/../detekt-baseline.xml")
}

tasks.withType(io.gitlab.arturbosch.detekt.Detekt).configureEach {
    jvmTarget = "17"  // Detekt doesn't support JVM 21 yet, use 17 for analysis
}

// Configure ktlint for code formatting
ktlint {
    version = "1.0.1"
    android = true
    ignoreFailures = false
    reporters {
        reporter "plain"
        reporter "checkstyle"
        reporter "sarif"
    }
}

android {
    if (project.android.hasProperty("namespace")) {
        namespace myLibraryGroup
    }

    compileSdkVersion myCompileSdk

    compileOptions {
        sourceCompatibility myJavaVersion
        targetCompatibility myJavaVersion
    }

    kotlinOptions {
        jvmTarget = myJvmTarget
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    defaultConfig {
        minSdkVersion myMinSdk
    }

    dependencies {
        testImplementation 'org.jetbrains.kotlin:kotlin-test'
        testImplementation 'org.mockito:mockito-core:5.0.0'

        implementation "androidx.media3:media3-exoplayer:1.3.1"
        implementation "androidx.media3:media3-ui:1.3.1"
        implementation "androidx.media3:media3-common:1.3.1"

        // AndroidX Media for MediaSession and MediaBrowserService (AAOS integration)
        implementation "androidx.media:media:1.7.0"
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()

            testLogging {
               events "passed", "skipped", "failed", "standardOut", "standardError"
               outputs.upToDateWhen {false}
               showStandardStreams = true
            }
        }
    }
}
